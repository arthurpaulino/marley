# Maintainer: katt <magunasu.b97@gmail.com>
# Maintainer: beaumanvienna (JC) <beaumanvienna@gmail.com>

pkgname=marley-git
pkgver=0.1.5
pkgrel=1
pkgdesc='A bundle of console emulators for Linux (git)'
arch=(x86_64)
url=https://github.com/beaumanvienna/marley
license=(GPL)
depends=(libaio\
         libjpeg-turbo\
         libpcap\
         libpulse\
         sdl2\
         boost\
         boost-libs\
         alsa-lib\
         bluez-libs\
         libevdev\
         libx11\
         libxi\
         libxrandr\
         lzo\
         mbedtls\
         libsndfile\
         mesa\
         libudev.so\
         libgl\
         glew\
         glibc\
         zlib\
         glu\
         libglvnd\
         python\
         freetype2\
         qt5-base\
         sfml\
         libavcodec.so\
         libminiupnpc.so\
         sdl2_image\
         sdl2_ttf\
         libpng\
         libsamplerate\
         wxgtk2\
         sndio\
         aom
         zip)
makedepends=(git cmake nasm)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(git+"${url}".git)
md5sums=('SKIP')

pkgver() {
	git -C "${pkgname%-git}" describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${pkgname%-git}"
	aclocal
	autoconf
	automake --add-missing --foreign
	./configure --prefix=/usr MAKEFLAGS=$MAKEFLAGS
	make
}

package() {
	cd "${pkgname%-git}"
	make DESTDIR="$pkgdir/" install
}
